plugins {
    id 'java'
    id 'checkstyle'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "xyz.jpenilla.run-paper" version "1.0.6"
    id 'net.minecrell.plugin-yml.bukkit' version '0.5.2'
}

repositories {
    mavenCentral()

    // Spigot
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }

    // Sonatype (kyori)
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

group = project.property("group")
version = project.property("version")
targetCompatibility = sourceCompatibility = JavaVersion.VERSION_17

bukkit {
    name = 'tatteltail'
    main = 'network.darkhelmet.tattletale.Tattletale'
    apiVersion = '1.17'
    authors = ['viveleroi']
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'
    compileOnly 'org.apache.logging.log4j:log4j-api:2.17.2'
    implementation 'net.kyori:adventure-api:4.11.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.1.1'
    implementation 'net.kyori:adventure-text-serializer-plain:4.11.0'
    implementation 'org.spongepowered:configurate-hocon:4.1.2'
}

jar {
    actions = []
    dependsOn = []
    dependsOn('shadowJar')
}

shadowJar {
    relocate 'net.kyori', 'network.darkhelmet.tattletale.kyori'
    relocate 'org.spongepowered', 'network.darkhelmet.tattletale.spongepowered'

    // Transitive deps
    relocate 'com.typesafe', 'network.darkhelmet.tattletale.typesafe'
    relocate 'io.leangen', 'network.darkhelmet.tattletale.leangen'

    minimize()
}

runServer {
    // Configure the Minecraft version for our task.
    // This is the only required configuration besides applying the plugin.
    // Your plugin's jar (or shadowJar if present) will be used automatically.
    minecraftVersion("1.19")
}